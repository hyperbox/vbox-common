apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.palantir.git-version'

group = 'io.kamax'
version = gitVersion()

sourceCompatibility = 1.7
targetCompatibility = 1.8

configurations {
    deployerJars
}

repositories {
    maven { url "https://kamax.io/maven/snapshots" }
    maven { url "https://kamax.io/maven/releases" }
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly 'io.kamax:hbox-api:0.0.14'
    compileOnly 'io.kamax:hbox-server-api:0.0.13-2-gcf752f2'
    compile project(':hbox-vbox-common-api')

    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
}

uploadArchives {
    gradle.ext.mavenUrl = System.env.MAVEN_URL + (project.version ==~ /\d+\.\d+\.\d+/ ? '/releases' : '/snapshots')
    gradle.ext.mavenUser = System.env.MAVEN_USER
    gradle.ext.mavenPassword = System.env.MAVEN_PASS
    gradle.ext.mavenPrivateKey = System.env.MAVEN_PK

    doLast {
        println "Published ${project.version} to maven repository ${gradle.mavenUrl} with user ${gradle.mavenUser}"
    }

    repositories.mavenDeployer {
        configuration = configurations.deployerJars

        repository(url: gradle.mavenUrl) {
            authentication(
                    userName: gradle.mavenUser,
                    password: gradle.mavenPassword,
                    privateKey: gradle.mavenPrivateKey
            )
        }
    }
}
